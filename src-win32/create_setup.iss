; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "BetterPoEditor"
#define MyAppVersion "1.0"
#define MyAppPublisher "Michele Locati"
#define MyAppURL "http://www.locati.it"
#define MyAppExeName "BetterPoEditor.exe"

#define DotNetRegSection HKEY_LOCAL_MACHINE
#define DotNetRegKey "Software\Microsoft\NET Framework Setup\NDP\v2.0.50727"
#define DotNetUrl "http://www.microsoft.com/downloads/details.aspx?FamilyID=0856eacb-4362-4b0d-8edd-aab15c5e04f5"

[Setup]
AppId={{9EA1210A-404D-4D38-A1CC-55ADDAC8A087}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={pf}\{#MyAppName}
DefaultGroupName={#MyAppName}
LicenseFile=..\licence.txt
OutputDir=..\bin-win32
OutputBaseFilename=bepoe_setup_{#MyAppVersion}
Compression=lzma
SolidCompression=yes
ChangesAssociations=yes

[Languages]
Name: english; MessagesFile: compiler:Default.isl

[Tasks]
Name: desktopicon; Description: {cm:CreateDesktopIcon}; GroupDescription: {cm:AdditionalIcons}; Flags: unchecked
Name: quicklaunchicon; Description: {cm:CreateQuickLaunchIcon}; GroupDescription: {cm:AdditionalIcons}; Flags: unchecked; OnlyBelowVersion: 0,6.1
Name: associatepo; Description: Open .po files with BetterPoEditor

[Files]
Source: ..\src\BetterPoEditor\bin\Release\BetterPoEditor.exe; DestDir: {app}; Flags: ignoreversion
Source: ..\src\BetterPoEditor\bin\Release\pofile.ico; DestDir: {app}; Flags: ignoreversion
Source: ..\src\Jayrock.Json\bin\Release\Jayrock.Json.dll; DestDir: {app}; Flags: ignoreversion
Source: ..\src\NetSpell.SpellChecker\bin\Release\NetSpell.SpellChecker.dll; DestDir: {app}; Flags: ignoreversion
Source: ..\src\NetSpell.DictionaryBuild\bin\Release\NetSpell.DictionaryBuild.exe; DestDir: {app}; Flags: ignoreversion
Source: ..\src\TidyNet\bin\Release\TidyNet.dll; DestDir: {app}; Flags: ignoreversion
Source: ..\src\BetterPoEditor\tools\*; DestDir: {app}\tools; Flags: ignoreversion recursesubdirs createallsubdirs
Source: ..\src\BetterPoEditor\dictionaries\*; DestDir: {app}\dictionaries; Flags: ignoreversion recursesubdirs createallsubdirs
Source: ..\contribs\*; DestDir: {app}\contribs; Flags: ignoreversion recursesubdirs createallsubdirs

[Icons]
Name: {group}\{#MyAppName}; Filename: {app}\{#MyAppExeName}
Name: {group}\Dictionaries\Readme; Filename: {app}\dictionaries\readme.htm
Name: {group}\Dictionaries\DictionaryBuild; Filename: {app}\NetSpell.DictionaryBuild.exe
Name: {group}\Dictionaries\Dictionaryies folder; Filename: {app}\dictionaries
Name: {group}\Contributions; Filename: {app}\contribs\readme.htm
Name: {group}\{cm:UninstallProgram,{#MyAppName}}; Filename: {uninstallexe}
Name: {commondesktop}\{#MyAppName}; Filename: {app}\{#MyAppExeName}; Tasks: desktopicon
Name: {userappdata}\Microsoft\Internet Explorer\Quick Launch\{#MyAppName}; Filename: {app}\{#MyAppExeName}; Tasks: quicklaunchicon

[Run]
Filename: {app}\{#MyAppExeName}; Description: {cm:LaunchProgram,{#MyAppName}}; Flags: nowait postinstall skipifsilent

[Code]
function InitializeSetup(): Boolean;
var
	version: cardinal;
	ErrCode: integer;
begin
	RegQueryDWordValue({#DotNetRegSection}, '{#DotNetRegKey}', 'Install', version);
	if version <> 1 then
	begin
		Result := False
		if MsgBox('This program requires that the Microsoft .Net Framework 2.0 is installed in your system.' #13#13 'Do you want to browse to its download web page?', mbError, MB_YESNO) = IDYES then
		begin
			if not ShellExec('open', '{#DotNetUrl}', '', '', SW_SHOW, ewNoWait, ErrCode) then
			begin
				MsgBox('The following error occurred while launching the browser:' #13 'Error number: ' + IntToStr(ErrCode) + '' #13 'Description: ' + SysErrorMessage(ErrCode) + '' #13#13 'Please download the Microsoft .NET Framework 2.0 Redistributable Package from' #13#13 '{#DotNetUrl}', mbCriticalError, MB_OK)
			end
		end
	end
	else
	begin
		Result := True
	end;
end;
[Registry]
Root: HKCR; Subkey: .po; ValueType: string; ValueName: ; ValueData: GettextFile; Flags: uninsdeletekey; Tasks: associatepo
Root: HKCR; Subkey: GettextFile; ValueType: string; ValueData: gettext message catalog; Flags: uninsdeletekey; Tasks: associatepo
Root: HKCR; Subkey: GettextFile\DefaultIcon; ValueType: string; ValueName: ; ValueData: {app}\pofile.ico; Tasks: associatepo
Root: HKCR; Subkey: GettextFile\shell\open\command; ValueType: string; ValueName: ; ValueData: """{app}\BetterPoEditor.exe"" ""%1"""; Tasks: associatepo
